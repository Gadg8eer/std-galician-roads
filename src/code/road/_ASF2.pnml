/*
#    OpenTTD Galician Roads, Trams & Vehicles (GALVET) -  Copyright (C) 2021  Pablo Villaverde Castro
#    This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
#    This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
#	 You should have received a copy of the GNU General Public License along with this program.  If not, see <https://github.com/pvillaverde/openttd-spanish-railsets/blob/main/LICENSE>.
*/
spriteset (RACN_guiSpriteset, "src/gfx/roads/RACN/gui.gimp.png") { roadGuiTemplate() }

spriteset (RACN_underlaySpriteset, "src/gfx/roads/RACN/underlay.gimp.png") { roadUnderlayTemplate() }
spriteset (RACN_underlaySnowSpriteset, "src/gfx/roads/RACN/underlaySnow.gimp.png") { roadUnderlayTemplate() }
spriteset (RACN_underlayAvenueSpriteset, "src/gfx/roads/RACN/underlayAvenue.gimp.png") { roadUnderlayTemplate() }
spriteset (RACN_underlayAvenueSnowSpriteset, "src/gfx/roads/RACN/underlayAvenueSnow.gimp.png") { roadUnderlayTemplate() }

spriteset (RACN_bridgesSpriteset, "src/gfx/roads/RACN/bridges.gimp.png") { roadBridgeOverlayTemplate() }
spriteset (RACN_bridgesSnowSpriteset, "src/gfx/roads/RACN/bridgesSnow.gimp.png") { roadBridgeOverlayTemplate() }

spriteset (RACN_stopsSpriteset, "src/gfx/roads/RACN/stops.gimp.png") { roadStopsTemplate() }
spriteset (RACN_stopsSnowSpriteset, "src/gfx/roads/RACN/stopsSnow.gimp.png") { roadStopsTemplate() }
spriteset (RACN_stopsCitySpriteset, "src/gfx/roads/RACN/stopsCity.gimp.png") { roadStopsTemplate() }
spriteset (RACN_stopsCitySnowSpriteset, "src/gfx/roads/RACN/stopsCitySnow.gimp.png") { roadStopsTemplate() }
spriteset (RACN_stopsAvenueSpriteset, "src/gfx/roads/RACN/stopsAvenue.gimp.png") { roadStopsTemplate() }
spriteset (RACN_stopsAvenueSnowSpriteset, "src/gfx/roads/RACN/stopsAvenueSnow.gimp.png") { roadStopsTemplate() }

spriteset (RACN_depotsSpriteset, "src/gfx/roads/RACN/depots.gimp.png") { roadDepotsTemplate() }
spriteset (RACN_depotsSnowSpriteset, "src/gfx/roads/RACN/depotsSnow.gimp.png") { roadDepotsTemplate() }


switch (FEAT_ROADTYPES, SELF, RACN_stopsTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_stopsSnowSpriteset;
	RACN_stopsSpriteset;
}
switch (FEAT_ROADTYPES, SELF, RACN_stopsCityTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_stopsCitySnowSpriteset;
	RACN_stopsCitySpriteset;
}
switch (FEAT_ROADTYPES, SELF, RACN_stopsAvenueTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_stopsAvenueSnowSpriteset;
	RACN_stopsAvenueSpriteset;
}
switch (FEAT_ROADTYPES, SELF, RACN_stopsTownZoneSwitch, town_zone) {
	TOWNZONE_CENTRE: RACN_stopsCityTerrainCheckSwitch;
	TOWNZONE_INNER_SUBURB: RACN_stopsCityTerrainCheckSwitch;
	TOWNZONE_OUTER_SUBURB: RACN_stopsAvenueTerrainCheckSwitch;
	TOWNZONE_OUTSKIRT: RACN_stopsAvenueTerrainCheckSwitch;
	RACN_stopsTerrainCheckSwitch;
}
switch (FEAT_ROADTYPES, SELF, RACN_bridgesTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_bridgesSnowSpriteset;
	RACN_bridgesSpriteset;
}
switch (FEAT_ROADTYPES, SELF, RACN_depotsTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_depotsSnowSpriteset;
	RACN_depotsSpriteset;
}
switch (FEAT_ROADTYPES, SELF, RACN_underlayAvenueTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_underlayAvenueSnowSpriteset;
	RACN_underlayAvenueSpriteset;
}
switch (FEAT_ROADTYPES, SELF, RACN_underlayTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: RACN_underlaySnowSpriteset;
	RACN_underlaySpriteset;
}

switch (FEAT_ROADTYPES, SELF, RACN_underlayTownZoneSwitch, town_zone) {
	TOWNZONE_CENTRE: RACN_underlayAvenueTerrainCheckSwitch;
	TOWNZONE_INNER_SUBURB: RACN_underlayAvenueTerrainCheckSwitch;
	TOWNZONE_OUTER_SUBURB: RACN_underlayAvenueTerrainCheckSwitch;
	TOWNZONE_OUTSKIRT: RACN_underlayAvenueTerrainCheckSwitch;
	RACN_underlayTerrainCheckSwitch;
}

if (param_speed_units == SPEED_UNITY_MPH) {
	item(FEAT_ROADTYPES, item_road_RACN) {
		property {	
			speed_limit:                 param_speedlimit_RACN mph;
		}
	}
}
if (param_speed_units == SPEED_UNITY_KMH) {
	item(FEAT_ROADTYPES, item_road_RACN) {
		property {	
			speed_limit:                 param_speedlimit_RACN km/h;
		}
	}
}
if (param_speed_units == SPEED_UNITY_MPS) {
	item(FEAT_ROADTYPES, item_road_RACN) {
		property {	
			speed_limit:                 param_speedlimit_RACN m/s;
		}
	}
}
item(FEAT_ROADTYPES, item_road_RACN) {
	property {
		label:                       "RACN";
		introduction_date:           (param_intro_year == 0) ? date(1960,01,01) : 0;
		name:                        string(STR_ROAD_RACN_NAME);
		toolbar_caption:             string(STR_ROAD_RACN_TOOLBAR);
		menu_text:                   string(STR_ROAD_RACN_MENU);
		build_window_caption:        string(STR_GENERIC_BUILD_VEHICLE);
		autoreplace_text:            string(STR_GENERIC_AUTOREPLACE);
		new_engine_text:             string(STR_GENERIC_NEW_ENGINE);
		powered_roadtype_list:       ["ROAD", "SETT", "STON", "R4X4", "DIRT", "GRAV", "ASF0", "EIND", "ASF1", "RACN", "HWAY"];
		roadtype_flags:              bitmask(ROADTYPE_FLAG_NO_LEVEL_CROSSING);
		construction_cost:           8;
		maintenance_cost:            16;
		sort_order:                  07;		
	}
	graphics {
		gui:						 RACN_guiSpriteset;
		underlay:                    RACN_underlayTownZoneSwitch;
		roadstops:                   RACN_stopsTownZoneSwitch;
		bridge_surfaces:             RACN_bridgesTerrainCheckSwitch;
		depots:	                     RACN_depotsTerrainCheckSwitch;
	}
}

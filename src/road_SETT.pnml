spriteset (SETT_guiSpriteset, "src/gfx/roads/SETT/gui.gimp.png") { roadGuiTemplate() }

spriteset (SETT_underlaySpriteset, "src/gfx/roads/SETT/underlay.gimp.png") { roadUnderlayTemplate() }
spriteset (SETT_underlaySnowSpriteset, "src/gfx/roads/SETT/underlaySnow.gimp.png") { roadUnderlayTemplate() }
spriteset (SETT_underlayCitySpriteset, "src/gfx/roads/SETT/underlayCity.gimp.png") { roadUnderlayTemplate() }

spriteset (SETT_bridgesSpriteset, "src/gfx/roads/SETT/bridges.gimp.png") { roadBridgeOverlayTemplate() }
spriteset (SETT_bridgesSnowSpriteset, "src/gfx/roads/SETT/bridgesSnow.gimp.png") { roadBridgeOverlayTemplate() }

spriteset (SETT_stopsSpriteset, "src/gfx/roads/SETT/stops.gimp.png") { roadStopsTemplate() }
spriteset (SETT_stopsSnowSpriteset, "src/gfx/roads/SETT/stopsSnow.gimp.png") { roadStopsTemplate() }

spriteset (SETT_depotsSpriteset, "src/gfx/roads/SETT/depots.gimp.png") { roadDepotsTemplate() }
spriteset (SETT_depotsSnowSpriteset, "src/gfx/roads/SETT/depotsSnow.gimp.png") { roadDepotsTemplate() }


switch (FEAT_ROADTYPES, SELF, SETT_stopsTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: SETT_stopsSnowSpriteset;
	SETT_stopsSpriteset;
}
switch (FEAT_ROADTYPES, SELF, SETT_bridgesTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: SETT_bridgesSnowSpriteset;
	SETT_bridgesSpriteset;
}
switch (FEAT_ROADTYPES, SELF, SETT_depotsTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: SETT_depotsSnowSpriteset;
	SETT_depotsSpriteset;
}

switch (FEAT_ROADTYPES, SELF, SETT_underlayTownZoneSwitch, town_zone) {
	TOWNZONE_CENTRE: SETT_underlayCitySpriteset;
	TOWNZONE_INNER_SUBURB: SETT_underlayCitySpriteset;
	TOWNZONE_OUTER_SUBURB: SETT_underlayCitySpriteset;
	TOWNZONE_OUTSKIRT: SETT_underlayCitySpriteset;
	SETT_underlaySpriteset;
}
switch (FEAT_ROADTYPES, SELF, SETT_underlayTerrainCheckSwitch, terrain_type) {
	TILETYPE_SNOW: SETT_underlaySnowSpriteset;
	SETT_underlayTownZoneSwitch;
}

if (param_speed_units == SPEED_UNITY_MPH) {
	item(FEAT_ROADTYPES, item_road_SETT) {
		property {	
			speed_limit:                 param_speedlimit_SETT mph;
		}
	}
}
if (param_speed_units == SPEED_UNITY_KMH) {
	item(FEAT_ROADTYPES, item_road_SETT) {
		property {	
			speed_limit:                 param_speedlimit_SETT km/h;
		}
	}
}
if (param_speed_units == SPEED_UNITY_MPS) {
	item(FEAT_ROADTYPES, item_road_SETT) {
		property {	
			speed_limit:                 param_speedlimit_SETT m/s;
		}
	}
}
item(FEAT_ROADTYPES, item_road_SETT) {
	property {
		label:                       "SETT";
		introduction_date:           0;
		name:                        string(STR_ROAD_SETT_NAME);
		toolbar_caption:             string(STR_ROAD_SETT_TOOLBAR);
		menu_text:                   string(STR_ROAD_SETT_MENU);
		build_window_caption:        string(STR_GENERIC_BUILD_VEHICLE);
		autoreplace_text:            string(STR_GENERIC_AUTOREPLACE);
		new_engine_text:             string(STR_GENERIC_NEW_ENGINE);
		powered_roadtype_list:       ["OLD_", "DIRT", "SETT", "ROAD", "ASP1", "ASP2", "HWAY", "ELSP", "ELRD", "ELA1", "ELA2", "ASP0"];
//		roadtype_flags:              bitmask(ROADTYPE_FLAG_TOWN_BUILD);
		construction_cost:           3; //Defaults: ROAD = 8, ELRD = 16, RAIL = 8, ELRL = 16
		maintenance_cost:            6; //Defaults: ROAD = 16, ELRD = 24, RAIL = 16, ELRL = 24		
		sort_order:                  02;		
	}
	graphics {
		gui:						 SETT_guiSpriteset;
		underlay:                    SETT_underlayTerrainCheckSwitch;
		roadstops:                   SETT_stopsTerrainCheckSwitch;
		bridge_surfaces:             SETT_bridgesTerrainCheckSwitch;
		depots:	                     SETT_depotsTerrainCheckSwitch;
	}
}